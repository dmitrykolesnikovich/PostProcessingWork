#include "Common.glsl"
#include "LightingCommon.glsl"
#include "ForwardShadowingCommon.glsl"

uniform float SkyLightVolumetricScatteringIntensity;
uniform float4 SkySH[3];
uniform bool UseHeightFogColors;
uniform bool UseDirectionalLightShadowing;
uniform float StaticLightingScatteringIntensity;
uniform float3 HeightFogDirectionalLightInscatteringColor;
uniform uint2 VolumetricFog_FogGridToPixelXY;

#ifndef ALLOW_SKY_LIGHTING
#define ALLOW_SKY_LIGHTING 0
#endif

layout(binding = 0, rgba16f) uniform readonly image3D LocalShadowedLightScattering;
layout(binding = 1, rgba16f) uniform readonly image3D VBufferA;
layout(binding = 2, rgba16f) uniform readonly image3D VBufferB;
layout(binding = 3, rgba16f) uniform writeonly image3D RWLightScattering;


uniform sampler3D LightScatteringHistory;

layout(local_size_x = THREADGROUP_SIZE, local_size_y = THREADGROUP_SIZE, local_size_z = THREADGROUP_SIZE)in;

void main()
{
    uint3 GridCoordinate = gl_GlobalInvocationID;
    float3 LightScattering = float3(0);
    uint NumSuperSamples = 1;

#if USE_TEMPORAL_REPROJECTION
    float3 HistoryUV = ComputeVolumeUV(ComputeCellWorldPosition(GridCoordinate, float3(.5f)), UnjitteredPrevWorldToClip);
    float HistoryAlpha = HistoryWeight;

//    FLATTEN
    if (any(lessThan(HistoryUV, float3(0))) || any(greaterThan(HistoryUV, float3(1))))
    {
        HistoryAlpha = 0;
    }

    // Supersample if the history was outside the camera frustum
    // The compute shader is dispatched with extra threads, make sure those don't supersample
    uint3 VolumetricFog_GridSizeInt= uint3(VolumetricFog_GridSize);
    NumSuperSamples = HistoryAlpha < .001f && all(lessThan(GridCoordinate, VolumetricFog_GridSizeInt)) ? HistoryMissSuperSampleCount : 1;
#endif

    for (uint SampleIndex = 0; SampleIndex < NumSuperSamples; SampleIndex++)
    {
        float3 CellOffset = FrameJitterOffsets[SampleIndex].xyz;
        //float CellOffset = .5f;

        float SceneDepth;
        float3 WorldPosition = ComputeCellWorldPosition(GridCoordinate, CellOffset, SceneDepth);
        float CameraVectorLength = length(WorldPosition - WorldCameraOrigin);
        float3 CameraVector = (WorldPosition - WorldCameraOrigin) / CameraVectorLength;

//        BRANCH
        if (ForwardLightData.HasDirectionalLight)
        {
            float ShadowFactor = 1;

            if (UseDirectionalLightShadowing)
            {
                ShadowFactor *= ComputeDirectionalLightStaticShadowing(WorldPosition);
                ShadowFactor *= ComputeDirectionalLightDynamicShadowing(WorldPosition, SceneDepth);
            }

            ShadowFactor *= GetLightFunction(WorldPosition);

            float3 DirectionalLightColor = ForwardLightData.DirectionalLightColor;

            if (UseHeightFogColors)
            {
                // Attempt to maintain intensity ratio between sky and sun
                DirectionalLightColor = HeightFogDirectionalLightInscatteringColor * Luminance(ForwardLightData.DirectionalLightColor);
            }

            LightScattering += DirectionalLightColor
            * (ShadowFactor
            * ForwardLightData.DirectionalLightVolumetricScatteringIntensity
            * PhaseFunction(PhaseG, dot(ForwardLightData.DirectionalLightDirection, -CameraVector)));
        }

        float3 BrickTextureUVs = float3(0);

#if (ALLOW_STATIC_LIGHTING || ALLOW_SKY_LIGHTING)
         FTwoBandSHVector RotatedHGZonalHarmonic;
        RotatedHGZonalHarmonic.V = float4(1.0f, CameraVector.y, CameraVector.z, CameraVector.x) * float4(1.0f, PhaseG, PhaseG, PhaseG);
#endif

#if ALLOW_STATIC_LIGHTING
        if (SkyLightVolumetricScatteringIntensity > 0 || StaticLightingScatteringIntensity > 0)
        {
            BrickTextureUVs = ComputeVolumetricLightmapBrickTextureUVs(WorldPosition);
        }
#endif

#if ALLOW_SKY_LIGHTING
        // Skylight
        if (SkyLightVolumetricScatteringIntensity > 0)
        {
            float3 SkyLighting;

            if (UseHeightFogColors > 0)
            {
                float3 HeightFogInscatteringColor = ComputeInscatteringColor(CameraVector, CameraVectorLength);
                float ScalarFactor = SHAmbientFunction();
                FTwoBandSHVectorRGB SkyIrradianceSH;
                SkyIrradianceSH.R.V = float4(ScalarFactor * HeightFogInscatteringColor.r, 0, 0, 0);
                SkyIrradianceSH.G.V = float4(ScalarFactor * HeightFogInscatteringColor.g, 0, 0, 0);
                SkyIrradianceSH.B.V = float4(ScalarFactor * HeightFogInscatteringColor.b, 0, 0, 0);

                SkyLighting = max(DotSH(SkyIrradianceSH, RotatedHGZonalHarmonic), 0);
            }
            else
            {
                FTwoBandSHVectorRGB SkyIrradianceSH;
                SkyIrradianceSH.R.V = SkySH[0];
                SkyIrradianceSH.G.V = SkySH[1];
                SkyIrradianceSH.B.V = SkySH[2];

                SkyLighting = View.SkyLightColor.rgb * max(DotSH(SkyIrradianceSH, RotatedHGZonalHarmonic), 0) / PI;
            }

            float SkyVisibility = ComputeSkyVisibility(WorldPosition, BrickTextureUVs);
            LightScattering += (SkyVisibility * SkyLightVolumetricScatteringIntensity) * SkyLighting;
        }
#endif

#if ALLOW_STATIC_LIGHTING
        // Indirect lighting of Stationary lights and Direct + Indirect lighting of Static lights
        if (StaticLightingScatteringIntensity > 0)
        {
            FTwoBandSHVectorRGB IrradianceSH = GetVolumetricLightmapSH2(BrickTextureUVs);

            LightScattering += (StaticLightingScatteringIntensity / PI) * max(DotSH(IrradianceSH, RotatedHGZonalHarmonic), 0);
        }
#endif

        uint GridIndex = ComputeLightGridCellIndex(GridCoordinate.xy * VolumetricFog_FogGridToPixelXY, SceneDepth, 0);
        const FCulledLightsGridData CulledLightsGrid = GetCulledLightsGrid(GridIndex, 0);

        float CellRadius = length(WorldPosition - ComputeCellWorldPosition(GridCoordinate + uint3(1, 1, 1), CellOffset));
        // Bias the inverse squared light falloff based on voxel size to prevent aliasing near the light source
        float DistanceBiasSqr = max(CellRadius * InverseSquaredLightDistanceBiasScale, 1);
        DistanceBiasSqr *= DistanceBiasSqr;
//        FRectTexture RectTexture = InitRectTexture(DeferredLightUniforms.SourceTexture);

        // Forward lighting of unshadowed point and spot lights
        for (uint LocalLightListIndex = 0; LocalLightListIndex < CulledLightsGrid.NumLocalLights; LocalLightListIndex++)
        {
            const FLocalLightData LocalLight = GetLocalLightData(CulledLightsGrid.DataStartIndex + LocalLightListIndex, 0);

            float VolumetricScatteringIntensity = f16tof32(asuint(LocalLight.SpotAnglesAndSourceRadiusPacked.w) >> 16);

            if (VolumetricScatteringIntensity > 0)
            {
                FDeferredLightData LightData /*= (FDeferredLightData)0*/;
                LightData.Position = LocalLight.LightPositionAndInvRadius.xyz;
                LightData.InvRadius = LocalLight.LightPositionAndInvRadius.w;
                LightData.Color = LocalLight.LightColorAndFalloffExponent.xyz;
                LightData.FalloffExponent = LocalLight.LightColorAndFalloffExponent.w;
                LightData.Direction = LocalLight.LightDirectionAndShadowMask.xyz;
                LightData.SpotAngles = LocalLight.SpotAnglesAndSourceRadiusPacked.xy;
                LightData.SourceRadius = LocalLight.SpotAnglesAndSourceRadiusPacked.z;
                LightData.SourceLength = f16tof32(asuint(LocalLight.SpotAnglesAndSourceRadiusPacked.w));
                LightData.Tangent = LocalLight.LightTangentAndSoftSourceRadius.xyz;
                LightData.SoftSourceRadius = LocalLight.LightTangentAndSoftSourceRadius.w;
                LightData.bInverseSquared = LightData.FalloffExponent == 0;
                LightData.bRadialLight = true;
                LightData.bSpotLight = LightData.SpotAngles.x > -2.0f;
                LightData.bRectLight = false;
                LightData.ShadowedBits = 0;

                float3 LightColor = LightData.Color;

                float3 L = float3(0);
                float3 ToLight = float3(0);
                float LightMask = GetLocalLightAttenuation(WorldPosition, LightData, ToLight, L);

                float Lighting;
                /*if( LightData.bRectLight )
                {
                    FRect Rect = GetRect( ToLight, LightData );

                    Lighting = IntegrateLight(Rect, RectTexture);
                }
                else*/
                {
                    FCapsuleLight Capsule = GetCapsule(ToLight, LightData);
                    Capsule.DistBiasSqr = DistanceBiasSqr;

                    Lighting = IntegrateLight(Capsule, LightData.bInverseSquared);
                }

                float CombinedAttenuation = Lighting * LightMask;

                LightScattering += LightColor * (PhaseFunction(PhaseG, dot(L, -CameraVector)) * CombinedAttenuation * VolumetricScatteringIntensity);

                // To debug culling
                //LightScattering += LocalLight.LightColorAndFalloffExponent.xyz * .0000001f;
            }
        }
    }

    LightScattering /= float(NumSuperSamples);

    // Shadowed point and spot lights were computed earlier
//    LightScattering += LocalShadowedLightScattering[GridCoordinate].xyz;
    LightScattering += imageLoad(LocalShadowedLightScattering, int3(GridCoordinate)).xyz;

    float4 MaterialScatteringAndAbsorption = /*VBufferA[GridCoordinate]*/ imageLoad(VBufferA, int3(GridCoordinate));
    float Extinction = MaterialScatteringAndAbsorption.w + Luminance(MaterialScatteringAndAbsorption.xyz);
    float3 MaterialEmissive = /*VBufferB[GridCoordinate].xyz*/imageLoad(VBufferB, int3(GridCoordinate)).xyz;
    float4 ScatteringAndExtinction = EncodeHDR(float4(LightScattering * MaterialScatteringAndAbsorption.xyz + MaterialEmissive, Extinction));

#if USE_TEMPORAL_REPROJECTION
//    BRANCH
    if (HistoryAlpha > 0)
    {
        float4 HistoryScatteringAndExtinction = Texture3DSampleLevel(LightScatteringHistory, HistoryUV, 0);
        ScatteringAndExtinction = lerp(ScatteringAndExtinction, HistoryScatteringAndExtinction, HistoryAlpha);
    }
#endif

    if (all(lessThan(GridCoordinate, VolumetricFog_GridSizeInt)))
    {
        ScatteringAndExtinction = MakePositiveFinite(ScatteringAndExtinction);
//        RWLightScattering[GridCoordinate] = ScatteringAndExtinction;
        imageStore(RWLightScattering, int3(GridCoordinate), ScatteringAndExtinction);
    }
}